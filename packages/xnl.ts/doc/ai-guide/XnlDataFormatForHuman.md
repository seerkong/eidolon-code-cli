# XNL 规格概要（人类版）
XNL（Extensible Notation Language）

## 基础约定
- 开始标签形态：`<name ...>`，内部可包含元数据 `key=value`、`{}` 属性块、`[]` 数组体、`()` 唯一子节点块，或 `#` 文本块。
- 结束符号：
  - 属性块：`{ ... }`
  - 数组块：`[ ... ]`
  - 唯一子节点块（extend）：`( ... )`（内部只能是标签，重复名覆盖旧值并产生警告）
  - 文本块：`<name ... #> ... <#>` 或带标记 `<name ... #flag> ... <#flag>`，结束标记无需节点名
  - 无内容节点：直接以 `>` 结束
- 标签名/键名沿用 XML 标识符；键可用单双引号包裹（允许空格等）。

## 节点结构
- `metadata`：开始标签内的 `key=value` 对，映射到 `Record<string, XnlNode>`。
- `attributes`：可选 `{ ... }` 块，`key = value` 形式，值同样是 `XnlNode`。
- `body`：可选 `[]` 块，元素为 `XnlNode`，可混合值/对象/数组/子节点/注释。
- `extend`：可选 `()` 块，必须是子节点列表，同名覆盖并告警，按出现顺序输出。
- `text`：可选文本块 `<name ... #marker?> ... <#marker?>`，允许 metadata/`{}`，禁止 `[]`/`()` 组合。
- `textMarker`：文本块的可选标记字符串（必须与结束标签匹配）。

## 字面量规则
- `ValueLiteral` 仅包含：字符串（单双引号，支持 `\\`、`\"`、`\'`、`\n`、`\t`、`\r`）、布尔、空值、数值（保留整数/浮点元数据）。
- 对象：`{ key = value ... }`，键为标识符或引号字符串，`value` 可为任意 `XnlNode`（值、对象、数组、元素、注释）。
- 数组：`[ value1 value2 ... ]`，元素同样是 `XnlNode`，允许混合值与子标签。
- 裸标识符作为字符串；无表达式/原始块语义（`()` 用于 extend）。

## 文本块去缩进
- 若文本块跨行：去掉首行空行，然后按结束 `<#...>` 前的缩进前缀（空格/Tab）对每行做去除，类似 C# 三引号。
- 文本内容内的 `<!-- -->` 注释会被移除。

## 注释
- `<!-- ... -->` 可出现在节点间、块内、文本块中；解析时跳过，不出现在 AST（文本中会被剔除）。

## 示例
```xnl
<doc [
  <no_body_node1>
  <no_body_node2 a=[1] b={c=3}>
  <metadata_demo1 xx=1 {
    a = 'abc'
    b = "tt\t\n"
    c = { inner = 2 }
    "string as key" = 2.3
    'string as key2' = <tt>
  }>
  <list_body1 [
    1 2 <item id="x" count=3 active=true note="hi">
  ]>
  <has_extend1 (
    <a {v=1}>
    <a {v=2}> <!-- 覆盖前一个 a，产生 DUPLICATE_CHILD 警告 -->
  )>
  <has_extend2 (
    <abc {
      a = [1 2]
      b = {c = 3}
    }>
    <efg [
      1
      <b>
    ]>
  )>
  <mixed_1 {
    a = 1
  } [
    1
    [2 3]
    <tt>
  ] (
    <abc {
      a = [1 2]
      b = {c = 3}
    }>
    <efg [
      1
      <b>
    ]>
  )>
  <text1 a=1 {b="zh"} #>
    在纯文本内部，无需转义，例如 & < > #
    可以包含形如 <notatag 的内容，均按文本处理
    多行文本会按结束标签所在行的缩进去除前缀
  <#>
  <text2 a=1 {b="cc"} #flag_1234>
    如果文本中包含 `<#>` 字样，可在开始标签后加标记，如 `#flag_1234`
    结束标签必须使用相同标记 `#flag_1234`
  <#flag_1234>
]>
```

## 解析与序列化要求
- 按块类型解析节点：metadata、`{}`、`[]`、`()`、`#`；文本块不得与 `[]`/`()` 同用。
- extend 块内部子节点同名覆盖旧值并产生 `DUPLICATE_CHILD` 警告，顺序跟随出现顺序。
- 值字面量按 JSON 风格解码，保留整数/浮点元数据；无表达式或原始块字面量。
- 文本块内容去缩进并移除注释；标记需首尾一致，否则报 `MISMATCHED_TAG`。
